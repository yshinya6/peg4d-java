TopLevel
    = Document
    
Chunk 
    = Document
    
File 
    = Document

Document
    = MISC* DocType MISC* 
    
    
DocType
    = '<!DOCTYPE' _* {@DocTypeName '[' (@Dtd)* #doctype } ']>'
    

Dtd
    =  _*  '<!' (Element/Attlist/Entity) '>'  _*
    

Element
	= _* 'ELEMENT' _* {@ElementName _*  ( '(' @Model (VS @Model)* ')' (@Regex)? / @OtherMember ) #element} _*
	

Attlist
	= _* 'ATTLIST' _* {@AttElementName _* (@AttParameter)* #attlist} _*
	
	
Entity
	= _* 'ENTITY' _* {@EntityName _* ('SYSTEM')? _* @String #entity} _*
	


//for Element
Model
	=  _* Member _* 
	
Member
	= { (@Data/@MemberName) (@Or)? ((VS)? (@Data/@MemberName) (@Or)?)* #member} 
	
Data
	= { '#PCDATA'  #data}
	
Regex
	= { ( '*' / '+' / '?') #regex}
	
Or
	= { _* '|' `/` _* #or}
	
OtherMember
	= { ('EMPTY' / 'ANY') #others}
	
	
//for Attlist
AttParameter
	= { _* @AttName _* @DataType _* (@Default)? _* #attParameter}
	 
DataType
	= { (@{'CDATA' #CDATA} 
		/ @{'IDREF' #IDREF} 
		/ @{'ID' #ID} 
		/ @{'ENTITY' #ENTITY} 
		/ @{'ENTITIES' #ENTITIES} 
		/ @{'NMTOKENS'} 
		/ @{'NMTOKEN' #NMTOKEN} 
		/ @Enumerated ) #dataType}
	
Enumerated
	= { '(' _* @{ NAME #enumMember } _* ( (@Or)  @{ NAME #enumMember } )+   _* ')'  _* #Enum }
	
Default
	= { @DefaultType  _* (@DefaultValue)? #default}
	
DefaultType
	= {('#REQUIRED' / '#IMPLIED' / '#FIXED' / STRING ) #defaultType}	
	
DefaultValue
	= {STRING #defaultValue}
	


//Name
AttName
	= {NAME #attName}
	
ElementName
	= {NAME #elementName}
	
AttElementName
	= {NAME #attElementName}
	
MemberName
	= {@Name (@Regex)? #memberName}
	
EntityName
	= {NAME #entityName}
	
DocTypeName
	= {NAME #docTypeName}
	

Name
	= { [A-Za-z:] ( '-' / [A-Za-z0-9:._])*  #name }

NAME
	=  [A-Za-z:] ( '-' / [A-Za-z0-9:._])*

String
    =  '"' { ( !'"' . )*  #string } '"'
    
STRING
	=  '"' ( !'"' . )* '"'
    


//Reference //TODO


MISC
    = COMMENT / _+
    

COMMENT
    = '<!--' (!'-->' . )* '-->'
    
    
VS
	= _* ',' _*
	

_ = [ \t\r\n]
